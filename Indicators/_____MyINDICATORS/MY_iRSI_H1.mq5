//+------------------------------------------------------------------+
//|                                                      MY_iRSI_H1.mq5 |
//|                                  Copyright 2025, MetaQuotes Ltd. |
//|                                             https://www.mql5.com |
//+------------------------------------------------------------------+
//#property copyright "Copyright 2025, MetaQuotes Ltd."
//#property link      "https://www.mql5.com"
//#property version   "1.00"
//#property indicator_chart_window
////+------------------------------------------------------------------+
////| Custom indicator initialization function                         |
////+------------------------------------------------------------------+
//int OnInit()
//  {
////--- indicator buffers mapping
//   
////---
//   return(INIT_SUCCEEDED);
//  }
////+------------------------------------------------------------------+
////| Custom indicator iteration function                              |
////+------------------------------------------------------------------+
//int OnCalculate(const int rates_total,
//                const int prev_calculated,
//                const datetime &time[],
//                const double &open[],
//                const double &high[],
//                const double &low[],
//                const double &close[],
//                const long &tick_volume[],
//                const long &volume[],
//                const int &spread[])
//  {
////---
//   
////--- return value of prev_calculated for next call
//   return(rates_total);
//  }
////+------------------------------------------------------------------+

#property copyright "Copyright 2000-2024, MetaQuotes Ltd." 
#property link      "https://www.mql5.com" 
#property version   "1.01" 
#property description "The indicator demonstrates how to obtain data" 
#property description "of indicator buffers for the iRSI technical indicator." 
#property description "A symbol and timeframe used for calculation of the indicator," 
#property description "are set by the symbol and period parameters." 
#property description "The method of creation of the handle is set through the 'type' parameter (function type)." 
#property description "All the other parameters are similar to the standard Relative Strength Index." 
  
#property indicator_separate_window 
#property indicator_buffers 1 
#property indicator_plots   1 
//--- drawing iRSI 
#property indicator_label1  "iRSI" 
#property indicator_type1   DRAW_LINE 
#property indicator_color1  clrDodgerBlue 
#property indicator_style1  STYLE_SOLID 
#property indicator_width1  1 
//--- limits for displaying of values in the indicator window 
#property indicator_maximum 100 
#property indicator_minimum 0 
//--- horizontal levels in the indicator window 
#property indicator_level1  70.0 
#property indicator_level2  30.0 
//+------------------------------------------------------------------+ 
//| Enumeration of the methods of handle creation                    | 
//+------------------------------------------------------------------+ 
enum Creation 
  { 
   Call_iRSI,              // use iRSI 
   Call_IndicatorCreate    // use IndicatorCreate 
  }; 
//--- input parameters 
input Creation             type=Call_iRSI;               // type of the function  
input int                  ma_period=13; // 14;                 // period of averaging 
// ? optimize later
input ENUM_APPLIED_PRICE   applied_price=PRICE_CLOSE;    // type of price  
input string               symbol=" ";                   // symbol  
// fix H1 ?
input ENUM_TIMEFRAMES      period=PERIOD_CURRENT;        // timeframe 
//--- indicator buffer 
double         iRSIBuffer[]; 
//--- variable for storing the handle of the iRSI indicator 
int    handle; 
//--- variable for storing 
string name=symbol; 
//--- name of the indicator on a chart 
string short_name; 
//--- we will keep the number of values in the Relative Strength Index indicator 
int    bars_calculated=0; 
//+------------------------------------------------------------------+ 
//| Custom indicator initialization function                         | 
//+------------------------------------------------------------------+ 
int OnInit() 
  { 
//--- assignment of array to indicator buffer 
   SetIndexBuffer(0,iRSIBuffer,INDICATOR_DATA); 
//--- determine the symbol the indicator is drawn for 
   name=symbol; 
//--- delete spaces to the right and to the left 
   StringTrimRight(name); 
   StringTrimLeft(name); 
//--- if it results in zero length of the 'name' string 
   if(StringLen(name)==0) 
     { 
      //--- take the symbol of the chart the indicator is attached to 
      name=_Symbol; 
     } 
//--- create handle of the indicator 
   if(type==Call_iRSI) 
      handle=iRSI(name,period,ma_period,applied_price); 
   else 
     { 
      //--- fill the structure with parameters of the indicator      
      MqlParam pars[2]; 
      //--- period of moving average 
      pars[0].type=TYPE_INT; 
      pars[0].integer_value=ma_period; 
      //--- limit of the step value that can be used for calculations 
      pars[1].type=TYPE_INT; 
      pars[1].integer_value=applied_price; 
      handle=IndicatorCreate(name,period,IND_RSI,2,pars); 
     } 
//--- if the handle is not created 
   if(handle==INVALID_HANDLE) 
     { 
      //--- tell about the failure and output the error code 
      PrintFormat("Failed to create handle of the iRSI indicator for the symbol %s/%s, error code %d", 
                  name, 
                  EnumToString(period), 
                  GetLastError()); 
      //--- the indicator is stopped early 
      return(INIT_FAILED); 
     } 
//--- show the symbol/timeframe the Relative Strength Index indicator is calculated for 
   short_name=StringFormat("iRSI(%s/%s, %d, %d)",name,EnumToString(period), 
                           ma_period,applied_price); 
   IndicatorSetString(INDICATOR_SHORTNAME,short_name); 
//--- normal initialization of the indicator 
   return(INIT_SUCCEEDED); 
  } 
//+------------------------------------------------------------------+ 
//| Custom indicator iteration function                              | 
//+------------------------------------------------------------------+ 
int OnCalculate(const int rates_total, 
                const int prev_calculated, 
                const datetime &time[], 
                const double &open[], 
                const double &high[], 
                const double &low[], 
                const double &close[], 
                const long &tick_volume[], 
                const long &volume[], 
                const int &spread[]) 
  { 
//--- number of values copied from the iRSI indicator 
   int values_to_copy; 
//--- determine the number of values calculated in the indicator 
   int calculated=BarsCalculated(handle); 
   if(calculated<=0) 
     { 
      PrintFormat("BarsCalculated() returned %d, error code %d",calculated,GetLastError()); 
      return(0); 
     } 
//--- if it is the first start of calculation of the indicator or if the number of values in the iRSI indicator changed 
//---or if it is necessary to calculated the indicator for two or more bars (it means something has changed in the price history) 
   if(prev_calculated==0 || calculated!=bars_calculated || rates_total>prev_calculated+1) 
     { 
      //--- if the iRSIBuffer array is greater than the number of values in the iRSI indicator for symbol/period, then we don't copy everything  
      //--- otherwise, we copy less than the size of indicator buffers 
      if(calculated>rates_total) values_to_copy=rates_total; 
      else                       values_to_copy=calculated; 
     } 
   else 
     { 
      //--- it means that it's not the first time of the indicator calculation, and since the last call of OnCalculate() 
      //--- for calculation not more than one bar is added 
      values_to_copy=(rates_total-prev_calculated)+1; 
     } 
//--- fill the array with values of the iRSI indicator 
//--- if FillArrayFromBuffer returns false, it means the information is nor ready yet, quit operation 
   if(!FillArrayFromBuffer(iRSIBuffer,handle,values_to_copy)) return(0); 
//--- form the message 
   string comm=StringFormat("%s ==>  Updated value in the indicator %s: %d", 
                            TimeToString(TimeCurrent(),TIME_DATE|TIME_SECONDS), 
                            short_name, 
                            values_to_copy); 
//--- display the service message on the chart 
   Comment(comm); 
//--- memorize the number of values in the Relative Strength Index indicator 
   bars_calculated=calculated; 
//--- return the prev_calculated value for the next call 
   return(rates_total); 
  } 
//+------------------------------------------------------------------+ 
//| Filling indicator buffers from the iRSI indicator                | 
//+------------------------------------------------------------------+ 
bool FillArrayFromBuffer(double &rsi_buffer[],  // indicator buffer of Relative Strength Index values 
                         int ind_handle,        // handle of the iRSI indicator 
                         int amount             // number of copied values 
                         ) 
  { 
//--- reset error code 
   ResetLastError(); 
//--- fill a part of the iRSIBuffer array with values from the indicator buffer that has 0 index 
   if(CopyBuffer(ind_handle,0,0,amount,rsi_buffer)<0) 
     { 
      //--- if the copying fails, tell the error code 
      PrintFormat("Failed to copy data from the iRSI indicator, error code %d",GetLastError()); 
      //--- quit with zero result - it means that the indicator is considered as not calculated 
      return(false); 
     } 
//--- everything is fine 
   return(true); 
  } 
//+------------------------------------------------------------------+ 
//| Indicator deinitialization function                              | 
//+------------------------------------------------------------------+ 
void OnDeinit(const int reason) 
  { 
   if(handle!=INVALID_HANDLE) 
      IndicatorRelease(handle); 
//--- clear the chart after deleting the indicator 
   Comment(""); 
  } 
